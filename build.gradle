apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'at.ac.tuwien.kr.alpha.Main'

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	antlr group: 'org.antlr', name: 'antlr4', version: '4.5.3'

	compile group: 'commons-cli',        name: 'commons-cli',          version: '1.3.1'
	compile group: 'org.slf4j',          name: 'slf4j-api',            version: '1.7.21'
	compile group: 'junit',              name: 'junit',                version: '4.12'
	compile group: 'net.sf.trove4j',     name: 'trove4j',              version: '3.0.3'
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
	compile group: 'org.apache.commons', name: 'commons-lang3',        version: '3.4'
	compile group: 'ch.qos.logback',     name: 'logback-classic',      version: '1.1.7'
	compile group: 'com.google.guava',   name: 'guava',                version: '20.0'

	// NOTE(flowlo): JGraphT was used in OMiGA, the predecessor of Alpha.
	// We might need it, so I'll keep this here just in case.
	//compile group: 'org.jgrapht', name: 'jgrapht-core', version: '0.9.2'
}

generateGrammarSource {
	// See https://github.com/antlr/antlr4/blob/master/doc/tool-options.md
	arguments += [
			"-visitor",
			"-no-listener",
			"-long-messages",
			"-package", "at.ac.tuwien.kr.alpha.antlr",
			"-Werror",
			"-Xlog"
	]
}

//* Toggle comment to get verbose [unchecked] warnings.
compileJava {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
compileTestJava {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}//*/

task bundledJar(type: Jar) {
	manifest {
		attributes 'Main-Class': mainClassName
	}

	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}

	archiveName = "${project.name}-bundled.jar"

	with jar
}