plugins {
	id 'antlr'
	id 'application'
	id 'checkstyle'
	id 'java'
	id 'jacoco'

	id 'com.github.kt3k.coveralls' version '2.8.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'at.ac.tuwien.kr.alpha.Main'

def antlrVersion = '4.7'
// Keep this in sync with antlr version to avoid multiple versions in classpath
def stringtemplateVersion = "4.0.8"

repositories {
	mavenCentral()
}

/* The following configuration directive is a work-around for a fault in the Gradle
 * ANTLR plugin. It would require both antlr4 and antlr4-runtime at compile time and
 * at run time, which unnecessarily bloats our JARs. Only antlr4-runtime is needed.
 * We therefore remove this extension of antlr dependencies being compile dependencies
 * and reintroduce them on our own.
 */
configurations {
	implementation {
		extendsFrom = extendsFrom.findAll { it != configurations.antlr }
	}
}

dependencies {
	// We need to give the ANTLR Plugin a hint.
	antlr group: 'org.antlr', name: 'antlr4', version: "${antlrVersion}"

	// Re-introduce antlr4-runtime as compile dependency.
	implementation group: 'org.antlr', name: 'antlr4-runtime', version: "${antlrVersion}"

	implementation group: 'ch.qos.logback',     name: 'logback-classic',      version: '1.1.7'
	implementation group: 'commons-cli',        name: 'commons-cli',          version: '1.3.1'
	implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
	implementation group: 'org.apache.commons', name: 'commons-lang3',        version: '3.6'
	implementation group: 'org.apache.commons', name: 'commons-text',         version: '1.8'
	implementation group: 'org.reflections',    name: 'reflections',          version: '0.9.11'
	implementation group: 'org.apache.poi',     name: 'poi',                  version: '4.1.1'
	implementation group: 'org.apache.poi',     name: 'poi-ooxml',            version: '4.1.1'
	implementation group: 'org.antlr',          name: 'ST4',                  version: '${stringtemplateVersion}$'

	testImplementation group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(AntlrTask) {
	// See https://github.com/antlr/antlr4/blob/master/doc/tool-options.md
	arguments += [
			"-visitor",
			"-no-listener",
			"-long-messages",
			"-package", "at.ac.tuwien.kr.alpha.antlr",
			"-Werror",
			"-Xlog",
			"-lib", "src/main/antlr/at/ac/tuwien/kr/alpha/antlr"
	]
}

compileJava {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

// Fix checkstyle version.
checkstyle {
	toolVersion = "7.6"
}


task bundledJar(type: Jar) {
	manifest {
		attributes 'Main-Class': mainClassName
	}

	from {
		configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}

	archiveFileName = "${project.name}-bundled.jar"
	
	/*
	 * In order to make sure we don't overwrite NOTICE and LICENSE files coming from dependency
	 * jars with each other, number them while copying
	 */
	int i = 1
	rename { name -> (name.equals("NOTICE.txt") || name.equals("NOTICE")) ? "NOTICE." + (i++) + ".txt" :  null }

	int j = 1
	rename { name -> (name.equals("LICENSE.txt") || name.equals("LICENSE")) ? "LICENSE." + (j++) + ".txt" : null }

	with jar
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}

	// NOTE: Contents of the antlr subpackage are autogenerated (see configuration of
	//       AntlrTasks above). It does not make sense to include them in our coverage
	//       report.
	afterEvaluate {
		getClassDirectories().setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: "at/ac/tuwien/kr/alpha/antlr/**")
		}))
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
	
}

wrapper {
	gradleVersion = '7.0'
	distributionType = 'ALL'
}
